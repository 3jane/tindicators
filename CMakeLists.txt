cmake_minimum_required(VERSION 3.12.4)
find_package(Python3 COMPONENTS Interpreter)
execute_process(
    COMMAND ${Python3_EXECUTABLE} -c "import version; print(version.version, end='')"
    OUTPUT_VARIABLE version
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
)

project(tulipindicators VERSION "${version}" LANGUAGES C CXX)

## Library
file(GLOB src indicators/*)
file(GLOB utils utils/*)
add_library(indicators SHARED ${src} ${utils} indicators.h indicators_index.c)
# target_link_libraries(indicators PUBLIC
    # "$<$<OR:$<C_COMPILER_ID:GNU>>:$<$<CONFIG:DEBUG>:asan;ubsan>;m>")
    # "$<$<OR:$<C_COMPILER_ID:Clang>,$<C_COMPILER_ID:AppleClang>,$<C_COMPILER_ID:GNU>>:$<$<CONFIG:DEBUG>:asan;ubsan>;m>")

target_link_libraries(indicators PUBLIC "$<$<C_COMPILER_ID:GNU>:$<$<CONFIG:DEBUG>:asan;ubsan>>")
target_link_libraries(indicators PUBLIC
    "$<$<OR:$<C_COMPILER_ID:Clang>,$<C_COMPILER_ID:AppleClang>,$<C_COMPILER_ID:GNU>>:m>")
target_link_options(indicators PUBLIC
    "$<$<OR:$<C_COMPILER_ID:Clang>,$<C_COMPILER_ID:AppleClang>,$<C_COMPILER_ID:GNU>>:$<$<CONFIG:DEBUG>:-fsanitize=undefined;-fsanitize=address;$<$<NOT:$<C_COMPILER_ID:AppleClang>>:-fsanitize=leak>>>")


# -fsanitize=address

set_target_properties(indicators PROPERTIES PUBLIC_HEADER "indicators.h")
install(TARGETS indicators
    PUBLIC_HEADER DESTINATION "include"
    ARCHIVE DESTINATION "lib"
    LIBRARY DESTINATION "lib")

target_compile_options(indicators
    PUBLIC
    $<$<OR:$<C_COMPILER_ID:Clang>,$<C_COMPILER_ID:AppleClang>,$<C_COMPILER_ID:GNU>>:
        -Wall -Wextra -Wshadow -g3 -ggdb -O2 -Werror=sequence-point -Werror=vla
        -Wno-unused-variable -Wno-float-conversion -Wno-unused-value -Wno-unused-parameter $<$<C_COMPILER_ID:GNU>:-Wno-maybe-uninitialized>
        $<$<COMPILE_LANGUAGE:CXX>:-std=c++17 -pedantic>
        $<$<COMPILE_LANGUAGE:C>:-std=c99 -pedantic>
        $<$<CONFIG:DEBUG>:
            -fsanitize=undefined -fsanitize=address $<$<NOT:$<C_COMPILER_ID:AppleClang>>:-fsanitize=leak>>
        $<$<CONFIG:RELEASE>:
            -ffast-math -fno-finite-math-only>>
    $<$<C_COMPILER_ID:MSVC>:
        /std:c++17
        $<$<CONFIG:RELEASE>:
            /fp:fast>>
    PRIVATE
    $<$<C_COMPILER_ID:MSVC>:
        /DBUILDING>
)

execute_process(
    COMMAND ${Python3_EXECUTABLE} -m pip install -r requirements.txt
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
)
execute_process(
    COMMAND ${Python3_EXECUTABLE} codegen.py
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
)

# Only test the newly added indicator, based on the branch name

if ("${branch_name}" STREQUAL "")
    execute_process(
        COMMAND git rev-parse --abbrev-ref HEAD
        OUTPUT_VARIABLE branch_name
    )
endif()
string(STRIP "${branch_name}" branch_name)

execute_process(
    COMMAND ${CMAKE_COMMAND} -E echo ${branch_name}
    COMMAND ${Python3_EXECUTABLE} -c "pass; \
        import re; \
        m = re.match('[^/]*/[^/]*/([a-zA-Z0-9]+)(-.*)?', input()); \
        import yaml, os.path; \
        indicators = yaml.safe_load(open(os.path.join('${CMAKE_CURRENT_SOURCE_DIR}','indicators.yaml'))); \
        print(m[1], end='') if m and m[1] in indicators else ''; \
    "
    OUTPUT_VARIABLE indicator_name
)

if (${disable_single_testing})
    set(indicator_name "")
endif()

if ("${indicator_name}" STREQUAL "")
    message("no specific indicator")
else()
    message("current indicator: ${indicator_name}")
endif()


find_program(VALGRIND_CMD valgrind)
if (NOT "${VALGRIND_CMD}" STREQUAL "VALGRIND_CMD-NOTFOUND")
    set(VALGRIND_CMD ${VALGRIND_CMD} --error-exitcode=1)
else()
    set(VALGRIND_CMD "")
endif()
if (CMAKE_BUILD_TYPE STREQUAL "Debug")
    set(VALGRIND_CMD "")
endif()
    
message("${VALGRIND_CMD}")

# Samples
# failed to build on MSVC:
# add_executable(example1 example1.c)
# target_link_libraries(example1 indicators)

add_executable(example2 example2.c)
target_link_libraries(example2 indicators)

add_executable(sample sample.c)
target_link_libraries(sample indicators)

# not ready yet:
# file(GLOB src-h utils/*.h indicators/*.h)
# add_custom_command(
#     OUTPUT tiamalgamation.c
#     COMMAND ${CMAKE_C_COMPILER} ${CMAKE_C_FLAGS} -E -P -DTI_SKIP_SYSTEM_HEADERS -D__TI_INDICATORS_H__ -D__BUFFER_H__ -D__MINMAX_H__ ${src} | cat - ${src-h} > tiamalgamation.c
# )

# CC -E -P $CFLAGS -DTI_SKIP_SYSTEM_HEADERS -D__TI_INDICATORS_H__ -D__BUFFER_H__ -D__MINMAX_H__ |
# cat <(CC -E -P $CFLAGS -DTI_SKIP_SYSTEM_HEADERS -D__TI_INDICATORS_H__ -D__BUFFER_H__ -D__MINMAX_H__) <(**/*.h) > tiamalgamation.c

## Tests
enable_testing()

add_executable(smoke smoke.c ${utils})
target_link_libraries(smoke indicators)
file(TO_NATIVE_PATH "${CMAKE_BINARY_DIR}/smoke" path)
add_test(NAME smoke COMMAND ${VALGRIND_CMD} ${path} ${indicator_name} WORKING_DIRECTORY ${CMAKE_SOURCE_DIR})

add_executable(fuzzer fuzzer.cc)
target_link_libraries(fuzzer indicators)
file(TO_NATIVE_PATH "${CMAKE_BINARY_DIR}/fuzzer" path)
add_test(NAME fuzzer COMMAND ${VALGRIND_CMD} ${path} ${indicator_name})

add_executable(benchmark benchmark.c)
set_target_properties(benchmark PROPERTIES EXCLUDE_FROM_ALL 1)
target_link_libraries(benchmark indicators ta_lib)
file(TO_NATIVE_PATH "${CMAKE_BINARY_DIR}/benchmark" path)
#add_test(NAME benchmark COMMAND ${VALGRIND_CMD} ${path} ${indicator_name})

add_executable(benchmark2 benchmark2.c ${utils})
target_link_libraries(benchmark2 indicators)
file(TO_NATIVE_PATH "${CMAKE_BINARY_DIR}/benchmark2" path)
if (NOT "${indicator_name}" STREQUAL "")
    add_test(NAME benchmark2 COMMAND ${VALGRIND_CMD} ${path} ${indicator_name})
else()
    add_test(NAME benchmark2 COMMAND                 ${path} ${indicator_name})
endif()

add_custom_target(check COMMAND ${VALGRIND_CMD} CTEST_OUTPUT_ON_FAILURE=1 ${CMAKE_CTEST_COMMAND})

## Bindings
add_custom_command(
    COMMAND echo 'dir=${CMAKE_BINARY_DIR}'
    COMMAND ls -lah ${CMAKE_BINARY_DIR}
    COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_BINARY_DIR}/libindicators.so ${CMAKE_SOURCE_DIR}/bindings/python/tulipindicators/
    COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_BINARY_DIR}/libindicators.dylib ${CMAKE_SOURCE_DIR}/bindings/python/tulipindicators/
    COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_BINARY_DIR}/indicators.dll ${CMAKE_SOURCE_DIR}/bindings/python/tulipindicators/
    COMMAND ls -lah ${CMAKE_SOURCE_DIR}/bindings/python/tulipindicators/
    COMMAND ${Python3_EXECUTABLE} -m pip install -e .[dev]
    COMMAND ${Python3_EXECUTABLE} setup.py bdist_wheel
    COMMAND ${Python3_EXECUTABLE} -m tox
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/bindings/python
    OUTPUT ${CMAKE_SOURCE_DIR}/bindings/python/dist/tulipindicators-${version}-py3-none-any.whl
)
add_custom_target(tulipindicators-python DEPENDS ${CMAKE_SOURCE_DIR}/bindings/python/dist/tulipindicators-${version}-py3-none-any.whl)
set_target_properties(tulipindicators-python PROPERTIES EXCLUDE_FROM_ALL 1)

add_custom_command(
    COMMAND ${Python3_EXECUTABLE} generate-bindings.py
    #COMMAND dotnet format
    COMMAND msbuild /t:restore
    COMMAND msbuild /t:pack /p:PackageVersion=${version} /p:Configuration=Release
    WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}/bindings/lean
    OUTPUT ${CMAKE_SOURCE_DIR}/bindings/lean/bin/Release/tulipindicators-lean.${version}.nupkg
)
add_custom_target(tulipindicators-lean DEPENDS tulipindicators-python ${CMAKE_SOURCE_DIR}/bindings/lean/bin/Release/tulipindicators-lean.${version}.nupkg)
set_target_properties(tulipindicators-lean PROPERTIES EXCLUDE_FROM_ALL 1)